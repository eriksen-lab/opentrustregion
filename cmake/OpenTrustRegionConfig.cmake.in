# OpenTrustRegionConfig.cmake
# ---------------------------
#
# OpenTrustRegion cmake module.
# This module sets the following variables in your project:
#
# ::
#
#   OpenTrustRegion_FOUND - true if OpenTrustRegion and all required components found on the system
#   OpenTrustRegion_VERSION - OpenTrustRegion version in format Major.Minor.Release
#
#
# Available components: shared static
#
# ::
#
#   shared - search for only shared library
#   static - search for only static library
#
#
# Exported targets:
#
# ::
#
# If OpenTrustRegion is found, this module defines the following :prop_tgt:`IMPORTED`
# targets. Target is shared _or_ static, so, for both, use separate, not overlapping, 
# installations. If no language components are requested, this module defines at least 
# the opentrustregion target. ::
#
#   OpenTrustRegion::OpenTrustRegion - the main OpenTrustRegion Fortran library with header & defs attached.
#
#
# Suggested usage:
#
# ::
#
#   find_package(OpenTrustRegion)
#   find_package(OpenTrustRegion 2.0.0 EXACT CONFIG REQUIRED COMPONENTS shared)
#
#
# The following variables can be set to guide the search for this package:
#
# ::
#
#   OpenTrustRegion_DIR - CMake variable, set to directory containing this Config file
#   CMAKE_PREFIX_PATH - CMake variable, set to root directory of this package
#   PATH - environment variable, set to bin directory of this package
#   CMAKE_DISABLE_FIND_PACKAGE_OpenTrustRegion - CMake variable, disables
#   find_package(OpenTrustRegion) when not REQUIRED, perhaps to force internal build

@PACKAGE_INIT@

set(otr OpenTrustRegion)
set (_valid_components
     static
     shared
)

# check library style component
if(@BUILD_SHARED_LIBS@)  # BUILD_SHARED_LIBS
    set(${otr}_shared_FOUND 1)
else()
    set(${otr}_static_FOUND 1)
endif()
list(FIND ${otr}_FIND_COMPONENTS "shared" _seek_shared)
list(FIND ${otr}_FIND_COMPONENTS "static" _seek_static)

check_required_components(${otr})

# don't include targets if this file is being picked up by another project which has 
# already built this as a subproject
if(NOT TARGET ${otr}::opentrustregion)

    include("${CMAKE_CURRENT_LIST_DIR}/${otr}Targets.cmake")

    if (CMAKE_VERSION VERSION_GREATER 3.15)
        set(_target "opentrustregion")
        get_property(_loc TARGET ${otr}::${_target} PROPERTY LOCATION)
        get_property(_ill TARGET ${otr}::${_target} PROPERTY INTERFACE_LINK_LIBRARIES)
        get_property(_id TARGET  ${otr}::${_target} PROPERTY INCLUDE_DIRECTORIES)
        get_property(_iid TARGET ${otr}::${_target} PROPERTY INTERFACE_INCLUDE_DIRECTORIES)

        message(VERBOSE "found: ${${otr}_FOUND} ver: ${${otr}_VERSION}")
        message(VERBOSE "${otr}::${_target}")
        message(VERBOSE "loc ${_loc}")
        message(VERBOSE "ill ${_ill}")
        message(VERBOSE "id  ${_id}")
        message(VERBOSE "iid ${_iid}")
    endif()
endif()

# add canonical alias (conditionally in case the package has already been included and 
# the alias has been created)
if(NOT TARGET ${otr}::${otr})
    add_library(${otr}::${otr}
                ALIAS ${otr}::opentrustregion
    )
endif()
